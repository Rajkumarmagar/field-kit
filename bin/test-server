#!/usr/bin/env coffee

fs   = require 'fs'
http = require 'http'
path = require 'path'

server = http.createServer (request, response) ->
  respond = (status, headers, data) ->
    if arguments.length is 2
      data = headers
      headers = undefined

    headers ||= {}
    response.writeHead status, headers
    response.end data
    console.log "#{request.method} #{request.url} #{status} #{data?.length or 0}"

  writeFileIfFound = (err, data) ->
    if err
      respond 404
    else
      respond 200, data

  serveFromPath = (root, requestPath) ->
    if not requestPath or requestPath.indexOf('..') > -1
      respond 404
    else
      filePath = path.join(__dirname, '..', root, requestPath)
      fs.readFile filePath, 'utf8', (err, data) ->
        if err
          fs.readFile path.join(filePath, 'index.html'), 'utf8', writeFileIfFound
        else
          writeFileIfFound err, data

  if request.url is '/'
    respond 302, Location: '/test'
  else if request.url.indexOf('/lib/') is 0
    requestPath = request.url.substring('/lib/'.length)
    serveFromPath 'lib', requestPath
  else
    requestPath = request.url.substring(1)
    serveFromPath 'public', requestPath

server.listen 9090
console.log "~ Listening at http://0.0.0.0:9090/"
