#!/usr/bin/env coffee

fs   = require 'fs'
http = require 'http'
path = require 'path'
Glue = require 'gluejs'
{exec} = require 'child_process'
CoffeeScript = require 'coffee-script'
PORT = 9090

server = http.createServer (request, response) ->
  respond = (status, headers, data) ->
    if arguments.length is 2 and typeof headers is 'string'
      data = headers
      headers = undefined

    headers ||= {}
    console.log "#{request.method} #{request.url} #{status} #{data?.length or 0}"
    response.writeHead status, headers
    response.end data

  writeFileIfFound = (err, data) ->
    if err
      respond 404
    else
      headers = {}
      if /\.(js|coffee)$/.test(request.url)
        headers['Content-Type'] = 'application/javascript'
      else if /\.html$/.test(request.url)
        headers['Content-Type'] = 'text/html'
      respond 200, headers, data

  serveFromPath = (root, requestPath) ->
    requestPath ||= '.'

    if not requestPath or requestPath.indexOf('..') > -1
      respond 404
    else
      filePath = path.join(__dirname, '..', root, requestPath)
      fs.readFile filePath, 'utf8', (err, data) ->
        if err
          fs.readFile path.join(filePath, 'index.html'), 'utf8', writeFileIfFound
        else
          if path.extname(requestPath) is '.coffee'
            data = CoffeeScript.compile data
          writeFileIfFound err, data

  if request.url is '/field-kit.js'
    new Glue().basepath('lib').include('lib').export('FieldKit').render writeFileIfFound
  if match = request.url.match(/^\/(lib|src)\//)
    requestPath = request.url.substring(match[0].length)
    serveFromPath match[1], requestPath
  else
    requestPath = request.url.substring(1)
    serveFromPath 'public', requestPath

server.listen PORT
url = "http://0.0.0.0:#{PORT}/"
console.log "~ Listening at #{url}"

exec "open #{url}"
